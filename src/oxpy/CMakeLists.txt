SET(oxpy_SOURCES
	OxpyContext.cpp
	OxpyManager.cpp
)

IF(Python)
	find_package(Python COMPONENTS Interpreter Development REQUIRED)
	add_subdirectory(pybind11 EXCLUDE_FROM_ALL) # the EXCLUDE_FROM_ALL forces cmake to use the local pybind11 files
	ADD_DEFINITIONS(-DPYTHON_BINDINGS)
	
	include_directories( ${PROJECT_SOURCE_DIR}/oxpy/pybind11/include )
	include_directories( ${PROJECT_SOURCE_DIR}/src )
	
	MESSAGE(STATUS "Enabling Python bindings")
	
	SET(CMAKE_SHARED_LIBRARY_PREFIX "")
	
	# we need the oxDNA "common" library to be compiled so that the resulting code is position-independent and the library can be linked dynamically
	set_target_properties(oxdna_common PROPERTIES POSITION_INDEPENDENT_CODE ON)
	
	Python_add_library(core MODULE bindings.cpp ${oxpy_SOURCES} WITH_SOABI)
	set_target_properties(core PROPERTIES POSITION_INDEPENDENT_CODE ON)
	target_include_directories(core PRIVATE ${Python_INCLUDE_DIRS})
	# On MacOS, Python extensions should NOT link against libpython
	# The symbols are resolved at runtime from the interpreter
	if(APPLE)
		target_link_libraries(core PUBLIC oxdna_common pybind11::headers)
		set_target_properties(core PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
	else()
		target_link_libraries(core PUBLIC oxdna_common pybind11::headers ${Python_LIBRARIES})
	endif()

	# Create a staging directory for the Python package
	set(OXPY_BASE_DIR "${CMAKE_BINARY_DIR}/python")
	set(OXPY_PACKAGE_DIR "${OXPY_BASE_DIR}/oxpy")

	# Make sure the directory exists
	file(MAKE_DIRECTORY "${OXPY_PACKAGE_DIR}")

	# Place the compiled extension (.so) in that folder
	set_target_properties(core
		PROPERTIES
			LIBRARY_OUTPUT_DIRECTORY "${OXPY_PACKAGE_DIR}"
			SUFFIX ".so"
	)

	# Copy the Python files into the staging directory
	configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/__init__.py ${OXPY_PACKAGE_DIR}/__init__.py COPYONLY)
	configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/utils.py   ${OXPY_PACKAGE_DIR}/utils.py COPYONLY)
	configure_file(${CMAKE_SOURCE_DIR}/README.md ${OXPY_PACKAGE_DIR}/README.md COPYONLY)
	configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/pyproject_cmake.toml ${OXPY_BASE_DIR}/pyproject.toml)
	configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/setup_cmake.py ${OXPY_BASE_DIR}/setup.py)

	if(SKBUILD)
		# copy the _version.py file generated by setuptools_scm
		configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/_version.py   ${OXPY_PACKAGE_DIR}/_version.py COPYONLY)
		# install the oxpy package to a location where scikit-build can find it
		install(DIRECTORY ${OXPY_PACKAGE_DIR}/
			DESTINATION oxpy
		)
	else()
		# Install the package using pip
		if(OxpySystemInstall)
			install(CODE "message(STATUS \"Installing oxpy system-wide via pip...\")
						execute_process(COMMAND ${Python_EXECUTABLE} -m pip install ${OXPY_BASE_DIR})")
		else()
			install(CODE "message(STATUS \"Installing oxpy for current user via pip...\")
						execute_process(COMMAND ${Python_EXECUTABLE} -m pip install --user ${OXPY_BASE_DIR})")
		endif()
	endif()
	
	# add a target to test oxpy
	add_custom_target(test_oxpy
	    ${PROJECT_SOURCE_DIR}/test/TestSuite.py test_folder_list.txt ${PYTHON_EXECUTABLE} oxpy
	    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}/test
	    COMMENT "Running oxpy tests" VERBATIM
	)
	# and one to test oat
	add_custom_target(test_oat
	    bash test.sh ${PYTHON_EXECUTABLE} && bash cleanup.sh
	    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}/analysis/tests
	    COMMENT "Running oxpy tests" VERBATIM
	)
	add_dependencies(test test_oxpy test_oat)
	
ENDIF(Python)
