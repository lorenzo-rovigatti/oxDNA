SET(oxpy_SOURCES
	OxpyContext.cpp
	OxpyManager.cpp
)

IF(Python)
	# First, check that the user's default Python meets the minimum version requirement.
	# Without this check, find_package may silently find a different Python installation
	# that meets the version requirement, causing oxpy to be installed for a Python
	# interpreter other than the one the user expects.
	find_program(DEFAULT_PYTHON NAMES python3 python)
	if(NOT DEFAULT_PYTHON)
		message(FATAL_ERROR "No python3 or python executable found in PATH.")
	endif()

	# Pin find_package to the user's default Python, preventing it from picking a different one.
	set(Python_EXECUTABLE "${DEFAULT_PYTHON}" CACHE FILEPATH "Path to Python interpreter" FORCE)

	if(OxpyVersionOverride)
		message(STATUS "OxpyVersionOverride is ON: skipping Python version check, using ${DEFAULT_PYTHON}")
		find_package(Python COMPONENTS Interpreter Development REQUIRED)
	else()
		execute_process(
			COMMAND "${DEFAULT_PYTHON}" -c "import sys; print(f'{sys.version_info.major}.{sys.version_info.minor}')"
			OUTPUT_VARIABLE DEFAULT_PYTHON_VERSION
			OUTPUT_STRIP_TRAILING_WHITESPACE
			RESULT_VARIABLE PYTHON_VERSION_RESULT
		)
		if(NOT PYTHON_VERSION_RESULT EQUAL 0)
			message(FATAL_ERROR "Failed to determine the version of ${DEFAULT_PYTHON}.")
		endif()

		if(DEFAULT_PYTHON_VERSION VERSION_LESS "3.11")
			message(FATAL_ERROR
				"Your default Python (${DEFAULT_PYTHON}) is version ${DEFAULT_PYTHON_VERSION}, "
				"but oxpy and oat require Python >= 3.11. Please upgrade your Python or activate an "
				"environment with a compatible version before building. "

				"If you need to run oxpy with Python < 3.11, please see install instructions in the documentation: "
				"https://lorenzo-rovigatti.github.io/oxDNA/install.html#using-oxpy-with-old-python-versions ")
		endif()

		find_package(Python 3.11 COMPONENTS Interpreter Development REQUIRED)
	endif()
	add_subdirectory(pybind11 EXCLUDE_FROM_ALL) # the EXCLUDE_FROM_ALL forces cmake to use the local pybind11 files
	ADD_DEFINITIONS(-DPYTHON_BINDINGS)
	
	include_directories( ${PROJECT_SOURCE_DIR}/oxpy/pybind11/include )
	include_directories( ${PROJECT_SOURCE_DIR}/src )
	
	MESSAGE(STATUS "Enabling Python bindings")
	
	SET(CMAKE_SHARED_LIBRARY_PREFIX "")
	
	# we need the oxDNA "common" library to be compiled so that the resulting code is position-independent and the library can be linked dynamically
	set_target_properties(oxdna_common PROPERTIES POSITION_INDEPENDENT_CODE ON)
	
	Python_add_library(core MODULE bindings.cpp ${oxpy_SOURCES} WITH_SOABI)
	set_target_properties(core PROPERTIES POSITION_INDEPENDENT_CODE ON)
	target_include_directories(core PRIVATE ${Python_INCLUDE_DIRS})
	# On MacOS, Python extensions should NOT link against libpython
	# The symbols are resolved at runtime from the interpreter
	if(APPLE)
		target_link_libraries(core PUBLIC oxdna_common pybind11::headers)
		set_target_properties(core PROPERTIES LINK_FLAGS "-undefined dynamic_lookup")
	else()
		target_link_libraries(core PUBLIC oxdna_common pybind11::headers ${Python_LIBRARIES})
	endif()

	# Create a staging directory for the Python package
	set(OXPY_BASE_DIR "${CMAKE_BINARY_DIR}/python")
	set(OXPY_PACKAGE_DIR "${OXPY_BASE_DIR}/oxpy")

	# Make sure the directory exists
	file(MAKE_DIRECTORY "${OXPY_PACKAGE_DIR}")

	# Place the compiled extension (.so) in that folder
	set_target_properties(core
		PROPERTIES
			LIBRARY_OUTPUT_DIRECTORY "${OXPY_PACKAGE_DIR}"
			SUFFIX ".so"
	)

	# Compute the oxpy version from the git tag (set by the top-level CMakeLists.txt)
	# Strip the leading 'v' if present (e.g. "v3.7.0" -> "3.7.0")
	string(REGEX REPLACE "^v" "" OXPY_VERSION "${tag}")

	# Copy the Python files into the staging directory
	configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/__init__.py ${OXPY_PACKAGE_DIR}/__init__.py COPYONLY)
	configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/utils.py   ${OXPY_PACKAGE_DIR}/utils.py COPYONLY)
	configure_file(${CMAKE_SOURCE_DIR}/README.md ${OXPY_PACKAGE_DIR}/README.md COPYONLY)
	# pyproject_cmake.toml uses @OXPY_VERSION@ substitution
	configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/pyproject_cmake.toml ${OXPY_BASE_DIR}/pyproject.toml @ONLY)
	# Note: no setup.py is needed; pyproject.toml is sufficient for modern pip
	# Generate _version.py so the installed package knows its version
	file(WRITE "${OXPY_PACKAGE_DIR}/_version.py" "version = '${OXPY_VERSION}'\n")

	if(SKBUILD)
		# copy the _version.py file generated by setuptools_scm
		configure_file(${CMAKE_SOURCE_DIR}/src/oxpy/_version.py   ${OXPY_PACKAGE_DIR}/_version.py COPYONLY)
		# install the oxpy package to a location where scikit-build can find it
		install(DIRECTORY ${OXPY_PACKAGE_DIR}/
			DESTINATION oxpy
		)
	else()
		# Install the package using pip
		if(OxpySystemInstall)
			install(CODE "message(STATUS \"Installing oxpy system-wide via pip...\")
						execute_process(COMMAND ${Python_EXECUTABLE} -m pip install ${OXPY_BASE_DIR})")
		else()
			install(CODE "message(STATUS \"Installing oxpy for current user via pip...\")
						execute_process(COMMAND ${Python_EXECUTABLE} -m pip install --user ${OXPY_BASE_DIR})")
		endif()
	endif()
	
	# add a target to test oxpy
	add_custom_target(test_oxpy
	    ${PROJECT_SOURCE_DIR}/test/TestSuite.py test_folder_list.txt ${PYTHON_EXECUTABLE} oxpy
	    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}/test
	    COMMENT "Running oxpy tests" VERBATIM
	)
	# and one to test oat
	add_custom_target(test_oat
	    pytest
	    WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}/analysis
	    COMMENT "Running oat tests" VERBATIM
	)
	add_dependencies(test test_oxpy test_oat)
	
ENDIF(Python)
